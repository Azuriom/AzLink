plugins {
    id 'fabric-loom' version '1.9.2'
    id 'com.gradleup.shadow' version '8.3.5'
}

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

ext {
    minecraft_version = '1.21.4'
    yarn_mappings = '1.21.4+build.8'
    loader_version = '0.16.14'
    fabric_version = '0.119.3+1.21.4'
}

dependencies {
    implementation project(':azlink-common')
    implementation 'net.kyori:adventure-api:4.21.0'
    implementation 'net.kyori:adventure-text-serializer-gson:4.21.0'
    implementation 'net.kyori:adventure-text-serializer-legacy:4.21.0'
    minecraft "com.mojang:minecraft:${project.ext.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.ext.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.ext.loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.ext.fabric_version}"
}

loom {
    runtimeOnlyLog4j = true
}

processResources {
    filesMatching('**/*.json') {
        expand 'version': project.version
    }
}

shadowJar {
    dependencies {
        exclude 'net.fabricmc:.*'
        include dependency('com.azuriom:.*')
        include dependency('net.kyori:.*')
    }

    relocate 'net.kyori', 'com.azuriom.azlink.libs'
}

remapJar {
    dependsOn tasks.shadowJar
    mustRunAfter tasks.shadowJar
    inputFile = shadowJar.archiveFile
    addNestedDependencies = true
    archiveFileName = "AzLink-Fabric-${project.version}-${project.ext.minecraft_version}.jar"
}

artifacts {
    archives remapJar
}
